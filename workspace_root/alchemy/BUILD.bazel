# Copyright 2022 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files", "strip_prefix")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//bazel/bash:defs.bzl", "custom_args_binary")

exports_files([
    "tools/bazel",
    "Cargo.toml",
    "Cargo.lock",
])

# TODO: Move to src/scripts/BUILD.bazel.
pkg_files(
    name = "scripts_src_files",
    srcs = glob(
        ["scripts/**"],
        exclude = ["scripts/.git/**"],
    ),
    attributes = pkg_attributes(
        mode = "0755",
    ),
    prefix = "/mnt/host/source/src",
    strip_prefix = strip_prefix.from_root(),
    visibility = ["//visibility:private"],
)

# TODO: Move to src/scripts/BUILD.bazel.
pkg_tar(
    name = "scripts_src",
    srcs = [
        ":scripts_src_files",
    ],
    compressor = "//bazel/portage/repo_defs/zstd:pzstd",
    extension = "tar.zst",
    visibility = ["@portage//images:__pkg__"],
)

# A convenient alias to run alchemist.
#
# Example:
#   bazel run //:alchemist -- --board=amd64-generic dump-package sys-apps/attr
alias(
    name = "alchemist",
    actual = "@alchemist//:alchemist",
    visibility = ["//visibility:private"],
)

# Generates rust-project.json and puts it in your workspace.
# This will get out of date, and will need to be rerun *approximately* whenever
# you change a rust target in your build file.
# For example, the following will require a rerun:
# * Adding a new dependency to a rust_* target
# * Creating a new rust_* target.
# * Changing compile configuration for a rust_* target.
# The following, on the other hand, will not require a rerun:
# * Modifying a rust file
# * Adding new source files to an existing target
custom_args_binary(
    name = "gen_rust_project",
    binary = "@rules_rust//tools/rust_analyzer:gen_rust_project",
    # This is a good default for users who don't know what they're doing.
    # While users may be able to optimize this by providing
    # //bazel/portage/bin/alchemist/..., for example, until
    # https://github.com/bazelbuild/bazel/issues/16310#issuecomment-2113976408
    # is supported, once we switch to cquery you won't pick up on the
    # transitive dependencies of alchemist, so we don't recommend this.
    binary_args = ["//bazel/..."],
)
