# Copyright 2022 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//bazel/portage/build_defs:sdk.bzl", "remote_toolchain_inputs", "sdk_from_archive", "sdk_update")

# When enabled, it will replace the stage1 SDK tarball with a fake one.
# This is useful when running a `bazel cquery` command because it won't force
# bazel to download the huge tarball.
bool_flag(
    name = "omit_stage1_tarball",
    build_setting_default = False,
)

config_setting(
    name = "omit_stage1_tarball_enabled",
    flag_values = {
        ":omit_stage1_tarball": "True",
    },
)

config_setting(
    name = "omit_stage1_tarball_disabled",
    flag_values = {
        ":omit_stage1_tarball": "False",
    },
)

pkg_tar(
    name = "extra_tarball",
    srcs = [
        "//bazel/portage/sdk/etc",
        "//bazel/portage/sdk/run/lock",
        "//bazel/portage/sdk/usr/bin",
        "//bazel/portage/sdk/usr/lib64",
        "//bazel/portage/sdk/usr/src",
    ],
    extension = "tar.gz",
    visibility = ["@portage//:__subpackages__"],
)

genrule(
    name = "fake_sdk_tarball",
    outs = ["fake_sdk_tarball.tar.gz"],
    cmd = "touch $@",
)

alias(
    name = "sdk_tarball",
    actual = select({
        "//bazel/portage:use_cros_bazel_sdk_disabled": "@files//:cros-sdk",
        "//bazel/portage:use_cros_bazel_sdk_enabled": "@files//:cros-bazel-sdk",
    }),
)

sdk_from_archive(
    name = "sdk_from_archive",
    src = select({
        ":omit_stage1_tarball_disabled": ":sdk_tarball",
        ":omit_stage1_tarball_enabled": ":fake_sdk_tarball",
    }),
    progress_message = "Extracting bootstrap SDK tarball",
    visibility = ["@//bazel:internal"],
)

remote_toolchain_inputs(
    name = "remote_toolchain_inputs",
    sdk = ":sdk_from_archive",
    visibility = ["@portage//:__subpackages__"],
)

# The stage1 bootstrap SDK.
# sdk_update will monkey patch portage and get the SDK setup so we can perform
# rootless builds.
sdk_update(
    name = "stage1",
    base = ":sdk_from_archive",
    extra_tarballs = [
        ":extra_tarball",
    ],
    visibility = [
        "@//bazel:internal",
        "@portage//:__subpackages__",
    ],
)
